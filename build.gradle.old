
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'eclipse'
}

allprojects {
    apply plugin: 'eclipse'

    group = 'me.realimpact.telecom.billing'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ["-Xlint:all,-processing"]
    }

    task printJavaVersion {
        doLast {
            println "Java Version used by Gradle: ${System.getProperty('java.version')}"
            println "Java Home used by Gradle: ${System.getProperty('java.home')}"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '21'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    tasks.named('eclipseJdt').configure {
        doLast {
            def jdtFile = file('.settings/org.eclipse.jdt.core.prefs')
            if (!jdtFile.exists()) {
                jdtFile.parentFile.mkdirs()
                jdtFile.createNewFile()
            }
            jdtFile.append('\norg.eclipse.jdt.core.encoding=UTF-8\n')
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        systemProperties = System.getProperties()
    }
}

project(':web-service') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

        // Calculation modules
        implementation project(':calculation:calculation-domain')
        implementation project(':calculation:calculation-api')
        implementation project(':calculation:calculation-port')
        implementation project(':calculation:calculation-application')
        implementation project(':calculation:calculation-infrastructure')

        // Policy modules (runtime implementation)
        implementation project(':calculation-policy-onetimecharge')
        implementation project(':calculation-policy-monthlyfee')
    }
}

project(':batch') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'

        // Calculation modules
        implementation project(':calculation:calculation-domain')
        implementation project(':calculation:calculation-api')
        implementation project(':calculation:calculation-port')
        implementation project(':calculation:calculation-application')
        implementation project(':calculation:calculation-infrastructure')

        // Policy modules (runtime implementation)
        implementation project(':calculation-policy-onetimecharge')
        implementation project(':calculation-policy-monthlyfee')

        runtimeOnly 'com.mysql:mysql-connector-j'
    }
}

project(':testgen') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
        runtimeOnly 'com.mysql:mysql-connector-j'
        implementation('com.github.javafaker:javafaker:1.0.2') {
            exclude group: 'org.yaml', module: 'snakeyaml'
        }
    }
}


